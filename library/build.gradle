apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.facebook.fbui.textlayoutbuilder:textlayoutbuilder:1.5.0'

    compileOnly 'androidx.constraintlayout:constraintlayout:1.1.3'
    compileOnly 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set("javadoc")
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

publish {
    userOrg = POM_DEVELOPER_ID
    groupId = GROUP
    artifactId = POM_ARTIFACT_ID
    publishVersion = VERSION_NAME
    uploadName = POM_NAME
    licences = ['Apache-2.0']
    desc = POM_DESCRIPTION
    website = POM_URL
    dryRun = false
    //bintrayUser =
    //bintrayKey = 
}

//./gradlew clean build bintrayUpload -PbintrayUser=BINTRAY_USERNAME -PbintrayKey=BINTRAY_KEY -PdryRun=false